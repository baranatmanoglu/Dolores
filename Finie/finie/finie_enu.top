topic: ~finie()
language: enu

# Defining extra concepts out of words or group of words
#concept:(hello) [hello hi hey "good morning" greetings]

# Catching inputs and triggering outputs
#u:(e:onStart) $onStopped=1

# Replying to speech
#u:(~hello) ~hello


concept:(finieStartProposal) You can ask me about your balance, income, transactions, spending history, spending advice, locations of ATMs and pay day. 
concept:(finieThinking) ["ok, thinking."]
concept:(noOneSpeak) ["You can talk to me."]
concept:(stillThere) ["Great."]
concept:(nextTime) ["Ok. Hope to see you again."]
concept:(exit) I want to [Exit Leave Abort Stop Quit]
concept:(yes) Yes
concept:(no) No
dynamic:tellerFinie
dynamic:offerFinie
concept:(offerYes) Great. I've started the process.
concept:(offerNo) Oh, Ok.
concept:(later) * [later "another time"]
concept:(offerLater) Ok. You can apply any time through IshCep.
concept:(whisper) This is a confidential information. I will whisper it to you. Come closer.
concept:(tryagain) Hey, I think I had a glitch. Can you ask me again?
concept:(nexttimeerror) I am sorry but I can not go on. Please go on with a different feature.
concept:(offerNU) You can simply say Yes or No

u:(e:Finie/TellResponse) $Finie/TellResponse ^gotoReactivate(finieContinue)

u:(e:Finie/WhisperResponse) \vol=50\ $Finie/WhisperResponse \vol=80\ ^gotoReactivate(finieContinue)

u:(e:Finie/TellResponseWithOffer) $Finie/TellResponseWithOffer \pau=1000\ ~offerFinie
    u1:(~yes) ~offerYes $Finie/ClearScreen=1 ^gotoReactivate(finieContinue)
    u1:(~no) ~offerNo $Finie/ClearScreen=1 ^gotoReactivate(finieContinue)
    u1:(~later) ~offerLater $Finie/ClearScreen=1 ^gotoReactivate(finieContinue)
    u1:(e:Dialog/NoOneSpeak10) ^gotoReactivate(finieContinue)
    u1:(e:Dialog/NotUnderstood2) ~offerNU ^stayInScope

u:(e:Finie/AcceptOffer) ~offerYes ^gotoReactivate(finieContinue)
u:(e:Finie/DeclineOffer) ~offerNo ^gotoReactivate(finieContinue)
u:(e:Finie/RemindOffer) ~offerLater ^gotoReactivate(finieContinue)

u:(~exit) ~nextTime $Finie/ExitApp=1

u:(e:Finie/FinieHasNoAnswer $Finie/FinieHasNoAnswer==0) ~tryagain ^gotoReactivate(finieContinue)

u:(e:Finie/FinieHasNoAnswer $Finie/FinieHasNoAnswer==1) ~nexttimeerror $Finie/ExitApp=1


proposal:%finieContinue  $Finie/Said=0
    u1:(_*)  ~finieThinking $Finie/StartSpeak=$1
    u1:(~exit) ~nextTime $Finie/ExitApp=1
    u1:(e:Dialog/NoOneSpeak10 $Finie/Said==0) $Finie/Said=1 ~noOneSpeak ^stayInScope
    u1:(e:Dialog/NoOneSpeak10 $Finie/Said==1) ~nextTime $Finie/ExitApp=1
    


proposal:%finieStart $Finie/Said=0 ~finieStartProposal $Finie/ReadyToGo=1
    u1:(_*)  ~finieThinking $Finie/StartSpeak=$1
    u1:(~exit) ~nextTime $Finie/ExitApp=1
    u1:(e:Dialog/NoOneSpeak10 $Finie/Said==0) $Finie/Said=1 ~noOneSpeak ^stayInScope
    u1:(e:Dialog/NoOneSpeak10 $Finie/Said==1) ~nextTime $Finie/ExitApp=1



proposal:%goToTeller ~tellerFinie \pau=3000\ $Finie/ExitApp=1

proposal:%finieWhisper ~whisper $Finie/PlayWhisper=1