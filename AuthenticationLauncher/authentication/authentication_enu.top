topic: ~authentication()
language: enu

# Defining extra concepts out of words or group of words
#concept:(hello) [hello hi hey "good morning" greetings]

# Catching inputs and triggering outputs
#u:(e:onStart) $onStopped=1

# Replying to speech
#u:(~hello) ~hello





concept:(proposeqr) You can scan your QR code. $Authentication/GlowQR=1
concept:(proposenfc) tap your contactless credit card here. $Authentication/GlowNFC=1
concept:(proposekeyboard) simply type your customer number. $Authentication/GlowKeyboard=1
concept:(proposelistener) or generate a sound code. $Authentication/GlowListener=1
concept:(stillThere) ["Great."]
concept:(nextTime) ["Ok. Hope to see you again."]
concept:(noOneSpeak) ["I couldn't get you."]

concept:(qrobject) ["Q R" barcode]
concept:(keyboardobject) [keyboard numpad "customer number"]
concept:(listenerobject) [sound noise phone]
concept:(nfcobject) ["N F C" "contactless card" "credit card" card]
concept:(qrinput) * ~qrobject
concept:(keyboardinput) * ~keyboardobject
concept:(listenerinput) * ~listenerobject
concept:(nfcinput) * ~nfcobject
concept:(yes) ["yes"]
concept:(no) ["no"]
concept:(reminder) ["Hey, can you choose how you want to authenticate yourself to me?"]
concept:(exitting) ["Ok. See you in another life brother."]
concept:(notunderstoodAuth2) You can simply say I want to tap my credit card. 

u:(e:Authentication/StartDialog) ^gotoReactivate(authenticationStart)

u:(e:Authentication/Reminder) ~reminder ^gotoReactivate(askForInput)

u:(e:Authentication/NoAction) ~exitting $Authentication/ExitApp=1


proposal:%authenticationStart $Authentication/Blocking=0 ^start(animations/Stand/Reactions/TouchHead_4)
    ~proposeqr
    ^wait(animations/Stand/Reactions/TouchHead_4) \pau=1000\
    ^gotoReactivate(proposalnfc)

proposal: %proposalnfc $Authentication/Next1=1

u:(e:Authentication/Next1 $Authentication/Blocking==0) ^start(animations/Stand/Gestures/This_1)
    ~proposenfc
    ^wait(animations/Stand/Gestures/This_1) \pau=1000\
    ^gotoReactivate(proposalkeyboard)

proposal: %proposalkeyboard $Authentication/Next2=1

u:(e:Authentication/Next2 $Authentication/Blocking==0) ^start(animations/Stand/Gestures/ShowTablet_1)
    ~proposekeyboard
    ^wait(animations/Stand/Gestures/ShowTablet_1) \pau=1000\
    ^gotoReactivate(proposallistener)

proposal: %proposallistener $Authentication/Next3=1

u:(e:Authentication/Next3 $Authentication/Blocking==0) ^start(animations/Stand/Reactions/TouchHead_4)
    ~proposelistener
    ^wait(animations/Stand/Reactions/TouchHead_4) $Authentication/EnableBasics=1 ^gotoReactivate(askForInput)
    
proposal: %askForInput
    u1:(~keyboardinput) Good choice. $Authentication/GoKeyboard=1
    u1:(~nfcinput) Good choice. $Authentication/GoNFC=1
    u1:(~qrinput) Good choice. $Authentication/GoQR=1
    u1:(~listenerinput) Good choice. $Authentication/GoListener=1
    u1:(e:Dialog/NotUnderstood2) ~notunderstoodAuth2 ^stayInScope
    



